Template:
  Name:                    FRPOP02
  Description:             Pop Up Prompter (Requests Search Values)

  Source: |
    @@COMMENT  COMMENT('=======================================================')
    @@COMMENT  COMMENT('Copyright .....: (C) @@COMPANY, 1991 ')
    @@COMMENT  COMMENT('Process .......: @@PROCESS ')
    @@COMMENT  COMMENT('Function ......: @@FUNCTION ')
    @@COMMENT  COMMENT('Created by ....: @@USER    ')
    @@COMMENT  COMMENT('Created on ....: @@DATE at @@TIME ')
    @@COMMENT  COMMENT('Description ...: @@FUNCDES ')
    @@COMMENT  COMMENT('=======================================================')
    /* ********** ------------------------------------------------        */
    /* ********** START OF TEMPLATE CODE                                  */
    /* ********** ------------------------------------------------        */
    /* ********** GET PREFIX FOR RDML VARIABLES                           */
    /* ********** ------------------------------------------------        */
    @@QUESTION PROMPT('Enter a 1 to 4 character prefix for variables declared by this template') ANSWER(@@CANS001) EXTEND('Specify a unique prefix (within this program) that can be used to ensure' 'that program variables generated by this template have unique names.' 'Use from 1 to 4 alphabetic charcters. Attempt to use a prefix that' 'identfies what this window will display. For example COMP for a list of' 'companies, CUST for a list of customers, etc.' 'For more information and examples use the HELP function key.') LOWER(*NO) HELPIDS(HELP010 HELP020 HELP030 HELP040)
    /* ********** ------------------------------------------------        */
    /* ********** ASK FOR HEADING OF WINDOW PANEL                         */
    /* ********** ------------------------------------------------        */
    @@QUESTION PROMPT('Enter the title of the POP UP window, up to 20 characters') ANSWER(@@CANS002) EXTEND('12345678901234567890' 'This will be used for the panel title that appears in the window.' '''Use upper and lower case characters. Do not use any '' (quote symbols).''' 'Use at most 20 characters.' 'For additional information and examples use the HELP function key.') HELPIDS(HELP010 HELP020 HELP030 HELP040)
    /* ********** ------------------------------------------------        */
    /* ********** GET FILE TO WORK WITH                                   */
    /* ********** ------------------------------------------------        */
    @@GET_FILS TO(1) PHY_ONLY(*NO) SGL_ONLY(*YES) PROMPT('Specify the name of the file to be displayed in this pop up window') EXTEND('The file name may be specified in full, partially (to cause a partial list' 'of available files to be displayed) or left blank (to cause a full list of' 'available files to be displayed). When a list of files is displayed, the' 'file required may be selected from the list.' ' ' 'Use the HELP function key for more details about this template and' 'examples of the type of RDML applications it can create.') HELPIDS(HELP010 HELP020 HELP030 HELP040)
    /* ********** ------------------------------------------------        */
    /* ********** SET UP BROWSE LIST                                      */
    /* ********** ------------------------------------------------        */
    @@CLR_LST  NUMBER(1)
    @@RTV_FLDS FROM_FILE(01) INTO_LST(1)
    @@CLR_LST  NUMBER(2)
    @@CLR_LST  NUMBER(3)
    @@MAK_LSTS FROM_LSTS(1) INTO_LSTS((2 'Fields to' 'be displayed') (3 'Fields to' 'Return')) HELPIDS(HELP010 HELP020 HELP030 HELP040)
    @@CLR_LST  NUMBER(4)
    @@RTV_KEYS OF_FILE(01) INTO_LST(4)
    @@CLR_LST  NUMBER(5)
    @@MRG_LSTS FROM_LSTS((4 *INPUT)) INTO_LST(5)
    @@CLR_LST  NUMBER(6)
    @@MRG_LSTS FROM_LSTS((2)) INTO_LST(6)
    @@MRG_LSTS FROM_LSTS((3 *HIDDEN)) INTO_LST(6)
    /* ********** ------------------------------------------------        */
    /* ********** END OF TEMPLATE CODE                                    */
    /* ********** ------------------------------------------------        */
    /* ********** START OF RDML CODE                                      */
    /* ********** ------------------------------------------------        */
    @@COMMENT  COMMENT('Function control options')
    FUNCTION   OPTIONS(*NOMESSAGES *DEFERWRITE *DIRECT *CLOSE_DISPLAY)
    @@COMMENT  COMMENT('Define work variables, groups and browselist')
    DEFINE     FIELD(#@@CANS001ENTRY) REFFLD(#LISTENTRY)
    DEF_LIST   NAME(#@@CANS001LIST) FIELDS(@@LST06) SEL_ENTRY(#@@CANS001ENTRY)
    GROUP_BY   NAME(#@@CANS001KEYS) FIELDS(@@LST05)
    @@COMMENT  COMMENT('Loop until terminated by CANCEL or selected entry')
    BEGIN_LOOP
    @@COMMENT  COMMENT('Get Search Criteria')
    POP_UP     FIELDS(#@@CANS001KEYS) AT_LOC(5 14) PANEL_TITL('@@CANS002') BROWSELIST(#@@CANS001LIST) EXIT_KEY(*NO) MENU_KEY(*YES *MENU)   PROMPT_KEY(*NO) DESIGN(*DOWN) IDENTIFY(*DESC)
    @@COMMENT  COMMENT('If an entry was selected')
    IF         COND('#@@CANS001ENTRY *GT 0')
    GET_ENTRY  NUMBER(#@@CANS001ENTRY) FROM_LIST(#@@CANS001LIST)
    EXCHANGE   FIELDS(@@LST03)
    MENU
    ENDIF
    @@COMMENT  COMMENT('Build list of records starting at key')
    CLR_LIST   #@@CANS001LIST
    SELECT     FIELDS(#@@CANS001LIST) FROM_FILE(@@FNAME01) WITH_KEY(@@LST04) NBR_KEYS(*COMPUTE) GENERIC(*YES)
    ADD_ENTRY  TO_LIST(#@@CANS001LIST)
    ENDSELECT
    @@COMMENT  COMMENT('Clear search args and loop back for another go')
    CHANGE     FIELD(#@@CANS001KEYS) TO(*NULL)
    END_LOOP
    /* ********** ------------------------------------------------        */
    /* ********** END   OF RDML CODE                                      */
    /* ********** ------------------------------------------------        */
    /* ********** FREE STORAGE - TEMPLATE PROCESSING                      */
    /* ********** ------------------------------------------------        */
    @@CLR_LST  NUMBER(1)
    @@CLR_LST  NUMBER(2)
    @@CLR_LST  NUMBER(3)
    @@CLR_LST  NUMBER(4)
    @@CLR_LST  NUMBER(5)
    @@CLR_LST  NUMBER(6)

  Help Panels:
  - HELP010:
      Description:        Pop up Prompter (with search)

      Text: |
        <-
                                                                                        
         This template can be used to create a more complex pop up prompter. Use this   
         template when the file from which the information is being extracted contains  
         more than 50 records. This window initially asks the user to specify what key  
         value(s) should be used to search the file. If you use this template you will  
         be asked the following questions ....                                          
                                                                                        
         1. To specify a unique prefix for variables declared by the template. Use a    
            name from 1 to 4 characters long and only use letters from the alphabet.    
            Try to make the name reflect what is in the window (eg: COMP, CUST).        
                                                                                        
         2. To specify a title that describes the type of information that will be      
            displayed in the pop up window. Use both upper and lower case characters,   
            restrict the title to no more than 20 characters and do not use ' (quotes). 
                                                                                        
         3. To nominate the file that contains the data you wish to display in the      
            pop up window. You can enter the full file name, a partial file name        
            (to get a partial list of available files), or leave the name blank (to     
            get a full list of available files). When a list of files is displayed      
            the one required may be chosen from the list.

  - HELP020:
      Description:        Pop up Prompter (with search)

      Text: |
        <-
                                                                                        
        4. Next, you must choose the fields that are to display in the pop up window.   
           Remember that a good looking window (ie: one without line wrap around) has   
           a finite width. So only select fields that the user really needs to see in   
           the window. Do not fill the window up with extraneous information.           
                                                                                        
           On the same screen you will be asked to choose the field(s) that should be   
           passed back to the routine that invoked the pop up window (ie: via the       
           exchange list facility).                                                     
                                                                                        
           Normally you would only choose the primary key(s) of the file being used.    
                                                                                        
           For instance, in a window showing customer details you would normally        
           exchange back the customer number (ie: the unique identifier of the customer 
           selected from the window). Similarly, in a window showing company details    
           you would exchange back the company number (ie: the unique identifier of the 
           company selected from the window).

  - HELP030:
      Description:        Pop up Prompter (with search)

      Text: |
        <-
                                                                                        
                                                                                        
                            ..........................................................  
                            : POPUP01             Choose Customer                    :  
                            :                                                        :  
                            : Customer state  . . . VIC                              :  
                            : Customer name . . . . SM                               :  
                            :                                                        :  
                            :                                                        :  
                            :                                                        :  
                            :                                                        :  
                                                                                     :  
          This is an example of what a pop up window                                 :  
          using this template over a logical file of                                 :  
          CUSTOMERS by STATE and NAME would look like                                :  
          when initially displayed. Note the title                                   :  
          and the request for the search key(s).                                     :  
                                                                                     :  
          This user wants to display all customers     ..............................:  
          in state VIC whose names start with SM.

  - HELP040:
      Description:        Pop up Prompter (with search)

      Text: |
        <-
                                                                                        
                                                                                        
                            ..........................................................  
                            : POPUP01             Choose Customer                    :  
                            :                                                        :  
                            : Customer state  . . . VIC                              :  
                            : Customer name . . . . SM                               :  
                            :                                                        :  
                            : Customer                                      Zip      :  
                            :  Number   Customer name                      Code      :  
                            :  017274   SMITH ENGINEERING PTY LIMITED      3674      :  
                                                       SALES INCORORATED   3647      :  
          This is an example of what the window would  TY LIMITED          3756      :  
          look like after a search has been made.      AND SONS            3009      :  
          The user can select the required customer,   BRICATIONS LIMITED  3067    + :  
          scroll to subsequent pages resulting from                                  :  
          the search, or request that another search   =Exit  F14=Msgs               :  
          be made using different search key(s).                                     :  
                                                       ..............................:
