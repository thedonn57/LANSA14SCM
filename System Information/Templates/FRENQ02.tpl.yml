Template:
  Name:                    FRENQ02
  Description:             Header/Detail Enquiry

  Source: |
    @@COMMENT  COMMENT('=======================================================')
    @@COMMENT  COMMENT('Copyright .....: (C) @@COMPANY, 1991 ')
    @@COMMENT  COMMENT('Process .......: @@PROCESS ')
    @@COMMENT  COMMENT('Function ......: @@FUNCTION ')
    @@COMMENT  COMMENT('Created by ....: @@USER    ')
    @@COMMENT  COMMENT('Created on ....: @@DATE at @@TIME ')
    @@COMMENT  COMMENT('Description ...: @@FUNCDES ')
    @@COMMENT  COMMENT('=======================================================')
    /* ********** ======================================================= */
    /* ********** GET NAMES OF UP TO 50 RELATED FILES                     */
    /* ********** ======================================================= */
    @@GET_FILS TO(50) PHY_ONLY(*NO) SGL_ONLY(*NO) PROMPT('Enter the name of the base file to be used by this template') EXTEND('The file name may be specified in full, partially (to cause a partial list' 'of available files to be displayed) or left blank (to cause a full list of' 'available files to be displayed). When a list of files is displayed, the' 'file required may be selected from the list.' ' ' 'Use the HELP function key for more details about this template and' 'examples of the type of RDML applications it can create.') HELPIDS(HELP010 HELP020 HELP030 HELP040)
    /* ********** ======================================================= */
    /* ********** ASK IF FUNCTION IS TO BE RUN UNDER "ACTION BAR"         */
    /* ********** ======================================================= */
    @@QUESTION PROMPT('Do you want this function to be part of an ACTION-BAR style process?') ANSWER(@@CANS091) EXTEND('Answer Y or N only.' 'If you specify Y, the cursor will be placed on the first field of the' 'screen(s) rather than in the action bar area.') LOWER(*NO) VALUES(Y N) HELPIDS(HELP010 HELP020 HELP030 HELP040)
    /* ********** ======================================================= */
    /* ********** LOAD DETAILS OF FIELDS OF "HEADER" INTO LIST 1          */
    /* ********** LOAD DETAILS OF FIELDS OF "BROWSE" INTO LIST 2          */
    /* ********** ======================================================= */
    @@CLR_LST  NUMBER(1)
    @@CLR_LST  NUMBER(2)
    @@SET_IDX  IDX_NAME(CF) TO(1)
    A10: @@LABEL
    @@CMP_IDX  IDX_NAME(CF) IDX_VALUE(@@TFMX) IF_GT(A20)
    @@IF       COND((*IF @@FAREACF *NE B)) GOTO(A12)
    @@RTV_FLDS FROM_FILE(CF) INTO_LST(2)
    @@GOTO     LABEL(A14)
    A12: @@RTV_FLDS FROM_FILE(CF) INTO_LST(1)
    A14: @@INC_IDX  IDX_NAME(CF)
    @@GOTO     LABEL(A10)
    A20: @@LABEL
    /* ********** ======================================================= */
    /* ********** ASK THE USER TO SELECT THE HEADER FIELDS REQUIRED       */
    /* ********** ======================================================= */
    @@CLR_LST  NUMBER(11)
    @@MAK_LSTS FROM_LSTS(1) INTO_LSTS((11 'Fields in' 'Header' 'Area' *SEQUENCE *ALL)) HELPIDS(HELP010 HELP020 HELP030 HELP040)
    /* ********** ======================================================= */
    /* ********** ASK THE USER TO SELECT THE BROWSE FIELDS REQUIRED       */
    /* ********** ======================================================= */
    @@CLR_LST  NUMBER(22)
    @@MAK_LSTS FROM_LSTS(2) INTO_LSTS((22 'Fields in' 'Detail/List' 'Area' *SEQUENCE *ALL)) HELPIDS(HELP010 HELP020 HELP030 HELP040)
    /* ********** ======================================================= */
    /* ********** ASK THE USER HOW TO DESIGN THE PANELS                   */
    /* ********** ======================================================= */
    @@QUESTION PROMPT('Design fields in the header area DOWN the screen or ACROSS the screen') ANSWER(@@CANS002) EXTEND('Reply DOWN or ACROSS only.' 'If your header area contains 10 (or less) fields, DOWN is the   ' 'recommended value. If your header area contains more than 10' 'fields, ACROSS is the recommended value.' 'Use the HELP function key for more information and examples.') LOWER(*NO) VALUES(DOWN ACROSS) HELPIDS(HELP010 HELP020 HELP030 HELP040)
    /* ********** ======================================================= */
    /* ********** MERGE ALL RELATED KEY FIELDS INTO LIST 11 OR LIST 22    */
    /* ********** AS *HIDDEN FIELDS. LIST 3 IS A WORKING LIST ONLY        */
    /* ********** ======================================================= */
    @@SET_IDX  IDX_NAME(CF) TO(2)
    A30: @@LABEL
    @@CMP_IDX  IDX_NAME(CF) IDX_VALUE(@@TFMX) IF_GT(A40)
    @@CLR_LST  NUMBER(3)
    @@RTV_RELN OF_FILE(CF) INTO_LST(3)
    @@IF       COND((*IF @@FAREACF *NE B)) GOTO(A34)
    @@IF       COND((*IF @@FRELRCF *EQ M)) GOTO(A34)
    @@MRG_LSTS FROM_LSTS((3 *HIDDEN)) INTO_LST(22)
    @@GOTO     LABEL(A36)
    A34: @@MRG_LSTS FROM_LSTS((3 *HIDDEN)) INTO_LST(11)
    A36: @@INC_IDX  IDX_NAME(CF)
    @@GOTO     LABEL(A30)
    A40: @@LABEL
    /* ********** ======================================================= */
    /* ********** GENERATION OF RDML CODE STARTS HERE                     */
    /* ********** ======================================================= */
    FUNCTION   OPTIONS(*NOMESSAGES *DEFERWRITE *DIRECT)
    GROUP_BY   NAME(#HEADER) FIELDS(@@LST11)
    DEF_LIST   NAME(#LIST) FIELDS((#LISTDUMMY *HIDDEN) @@LST22)
    @@COMMENT  'Loop until user EXITs or CANCELs'
    BEGIN_LOOP
    /* ********** ======================================================= */
    /* ********** REQUEST KEYS OF THE BASE FILE BE INPUT AND GET DATA     */
    /* ********** ======================================================= */
    @@CLR_LST  NUMBER(3)
    @@RTV_KEYS OF_FILE(1) INTO_LST(3)
    @@IF       COND((*IF @@CANS002 *NE DOWN)) GOTO(R01)
    /* ********** Request and Display with action bar needs CURSOR_LOC    */
    @@IF       COND((*IF @@CANS091 *EQ Y)) GOTO(L91)
    R10: REQUEST    FIELDS(@@LST03) DESIGN(*DOWN) IDENTIFY(*DESC)
    @@GOTO     LABEL(R02)
    L91: @@LABEL
    R10: REQUEST    FIELDS(@@LST03) DESIGN(*DOWN) IDENTIFY(*DESC) CURSOR_LOC(*ATFIELD #@@LEL0301)
    @@GOTO     LABEL(R02)
    R01: @@IF       COND((*IF @@CANS002 *NE ACROSS)) GOTO(R02)
    @@IF       COND((*IF @@CANS091 *EQ Y)) GOTO(L93)
    R10: REQUEST    FIELDS(@@LST03) DESIGN(*ACROSS) IDENTIFY(*LABEL)
    @@GOTO     LABEL(R02)
    L93: @@LABEL
    R10: REQUEST    FIELDS(@@LST03) DESIGN(*ACROSS) IDENTIFY(*LABEL) CURSOR_LOC(*ATFIELD #@@LEL0301)
    /* ********** ======================================================= */
    /* ********** GENERATE FETCH TO THE PRIMARY FILE                      */
    /* ********** ======================================================= */
    R02: @@COMMENT  COMMENT('Fetch file @@FNAME01 details     ')
    FETCH      FIELDS((#HEADER))    FROM_FILE(@@FNAME01) WITH_KEY(@@LST03) NOT_FOUND(R10) ISSUE_MSG(*YES)
    /* ********** ======================================================= */
    /* ********** GENERATE FETCHES TO ALL FILES IN THE HEADER AREA        */
    /* ********** ======================================================= */
    @@SET_IDX  IDX_NAME(CF) TO(2)
    H10: @@LABEL
    @@CMP_IDX  IDX_NAME(CF) IDX_VALUE(@@TFMX) IF_GT(H20)
    @@IF       COND((*IF @@FAREACF *EQ B)) GOTO(H15)
    @@CLR_LST  NUMBER(3)
    @@RTV_RELN OF_FILE(CF) INTO_LST(3)
    @@COMMENT  COMMENT('Fetch file @@FNAMECF details     ')
    FETCH      FIELDS((#HEADER)) FROM_FILE(@@FNAMECF) WITH_KEY(@@LST03)
    H15: @@INC_IDX  IDX_NAME(CF)
    @@GOTO     LABEL(H10)
    H20: @@LABEL
    /* ********** ======================================================= */
    /* ********** NOW EXTRACT DATA TO BE PLACED INTO THE BROWSE LIST      */
    /* ********** ======================================================= */
    @@SET_IDX  IDX_NAME(CF) TO(2)
    @@SET_IDX  IDX_NAME(SC) TO(0)
    A50: @@LABEL
    @@CMP_IDX  IDX_NAME(CF) IDX_VALUE(@@TFMX) IF_GT(A80)
    @@IF       COND((*IF @@FAREACF *NE B)) GOTO(A78)
    @@CLR_LST  NUMBER(3)
    @@RTV_RELN OF_FILE(CF) INTO_LST(3)
    @@IF       COND((*IF @@FRELRCF *EQ M)) GOTO(A55)
    /* ********** FETCH INTO THE LIST ENTRY                               */
    @@COMMENT  COMMENT('Fetch file @@FNAMECF details     ')
    FETCH      FIELDS((#LIST)) FROM_FILE(@@FNAMECF) WITH_KEY(@@LST03)
    @@GOTO     LABEL(A78)
    /* ********** THE ONE AND ONLY SELECT COMMAND                         */
    A55: @@COMMENT  COMMENT('Select all file @@FNAMECF details')
    @@INC_IDX  IDX_NAME(SC)
    SELECT     FIELDS((#LIST)) FROM_FILE(@@FNAMECF) WITH_KEY(@@LST03)
    @@GOTO     LABEL(A78)
    /* ********** INC INDEX AND LOOP AROUND                               */
    A78: @@INC_IDX  IDX_NAME(CF)
    @@GOTO     LABEL(A50)
    A80: @@LABEL
    /* ********** ======================================================= */
    /* ********** ADD_ENTRY AND ENDSELECT FOR THE LIST (IF SELECT USED)   */
    /* ********** ======================================================= */
    @@CMP_IDX  IDX_NAME(SC) IDX_VALUE(0) IF_EQ(A90)
    ADD_ENTRY  TO_LIST(#LIST)
    ENDSELECT
    A90: @@LABEL
    /* ********** ======================================================= */
    /* ********** DISPLAY DETAILS TO THE USER                             */
    /* ********** ======================================================= */
    @@COMMENT  COMMENT('Display results to the user')
    @@IF       COND((*IF @@CANS002 *NE DOWN)) GOTO(D01)
    /* ********** Request and Display with action bar needs CURSOR_LOC    */
    @@IF       COND((*IF @@CANS091 *EQ Y)) GOTO(L94)
    DISPLAY    FIELDS((#HEADER)) DESIGN(*DOWN) IDENTIFY(*DESC) BROWSELIST(#LIST)
    @@GOTO     LABEL(D02)
    L94: @@LABEL
    DISPLAY    FIELDS((#HEADER)) DESIGN(*DOWN) IDENTIFY(*DESC) BROWSELIST(#LIST) CURSOR_LOC(*ATFIELD #@@LEL1101)
    @@GOTO     LABEL(D02)
    D01: @@IF       COND((*IF @@CANS002 *NE ACROSS)) GOTO(D02)
    @@IF       COND((*IF @@CANS091 *EQ Y)) GOTO(L95)
    DISPLAY    FIELDS((#HEADER)) DESIGN(*ACROSS) IDENTIFY(*LABEL) BROWSELIST(#LIST)
    @@GOTO     LABEL(D02)
    L95: @@LABEL
    DISPLAY    FIELDS((#HEADER)) DESIGN(*ACROSS) IDENTIFY(*LABEL) BROWSELIST(#LIST) CURSOR_LOC(*ATFIELD #@@LEL1101)
    D02: @@COMMENT  COMMENT('Clear header and list and loop around ')
    CHANGE     FIELD(#HEADER) TO(*DEFAULT)
    @@CMP_IDX  IDX_NAME(SC) IDX_VALUE(0) IF_EQ(A95)
    CLR_LIST   NAMED(#LIST)
    A95: @@LABEL
    END_LOOP
    /* ********** ======================================================= */
    /* ********** END OF RDML COMMANDS                                    */
    /* ********** ======================================================= */
    @@CLR_LST  NUMBER(1)
    @@CLR_LST  NUMBER(2)
    @@CLR_LST  NUMBER(3)
    @@CLR_LST  NUMBER(11)
    @@CLR_LST  NUMBER(22)

  Help Panels:
  - HELP010:
      Description:        " Header/Detail Style Enquiry"

      Text: |
        <-
                                                                                        
         This template can be used to create a header / detail style enquiry.           
                                                                                        
         A header / detail style enquiry panel typically has a header area containing   
         details from the file upon which the template is based, and details from other 
         files that are in 1:1 relationships with the base file.                        
                                                                                        
         Additionally it has a detail or list area containing multiple occurences of    
         information from a file that is associated with the base file in a 1:many type 
         of relationship. Additionally, details from other files associated in 1:1      
         relationships with the main file in the list area may be displayed.            
                                                                                        
         This style of enquiry is very widely used. Some examples might include ....    
                                                                                        
              a. Enquire on an order and show a list of all associated line items       
                                                                                        
              b. Enquire on an employee and show a list of all acquired skills          
                                                                                        
              c. Enquire on a GL account and show a list of all transactions

  - HELP020:
      Description:        " Header/Detail Style Enquiry"

      Text: |
        <-
                                                                                        
         If you elect to use this template you will be asked ....                       
                                                                                        
         1. To nominate the file upon which this enquiry is to be based. The file named 
            may be a physical file or a logical file. You may specify the file name in  
            full, in part (to select from a partial list of all available files), or    
            leave the name blank (to select from a list of all available files).        
            Once the base file has been chosen, other associated files may be chosen.   
            Information from these other files may be presented on the header or detail 
            area of the enquiry screen. This decision is made automatically by the      
            template according to the files relationship with the base file.            
                                                                                        
         2. To choose and order the fields that you wish to appear in the header area   
            of the enquiry panel.                                                       
                                                                                        
         3. To choose and order the fields that you wish to appear in the detail area   
            of the enquiry panel.                                                       
                                                                                        
         4. To specify whether fields in the header area are to be designed ACROSS the  
            enquiry panel or DOWN the enquiry panel.

  - HELP030:
      Description:        " Header/Detail Style Enquiry"

      Text: |
        <-
                                                                                        
         Order number . . . 300987                                                      
                                                                                        
                                                                                        
                                                                                        
                                                                                        
                                                                                        
                                                                                        
                                                                                        
                                                                                        
                                                                                        
                                                This is an example of the first panel   
                                                that would be presented by a program    
                                                created from this template over an      
                                                ORDERS file.                            
                                                                                        
                                                It is requesting that the user specify  
                                                the number of the ORDER that is to be   
                                                displayed. The user has entered 300987

  - HELP040:
      Description:        " Header/Detail Style Enquiry"

      Text: |
        <-
                                                                                        
         Order number . . . . 300987                                                    
         Date due . . . . . . 11/21/89                                                  
         Customer no  . . . . 75691                                                     
         Customer name  . . . ACME TOOL AND DIE COMPANY                                 
         City / state . . . . CHICAGO / ILLINOIS                                        
         Phone number . . . . (312) 647-4377                                            
                                                                                        
         Product                            Quantity           Unit       Total         
         Number   Description               Ordered    Unit    Price      Price         
         0102029  1" Galvanized Hex Nuts      1,000    BOX      1.14   1,140.00         
         0303948  Bolt Cutters                    5    UNIT    31.00     155.00         
         0485786  High Tension Cable                                                    
         0979794  Acetylene Hose                This is an example of the full enquiry  
         4048487  1/2" Ring Spanner             panel. Note the header area that has    
         4944048  Ball and Pane Hammer          details from the ORDERS file and from   
         3039393  Asbestos Gloves               the CUSTOMERS file.                     
         9303030  Tape Measure                  The detail area has details from the    
         2828393  Nylon String                  LINE ITEMS file and the PRODUCT file.   
         3930304  Heat Shrinkable Wrapping
