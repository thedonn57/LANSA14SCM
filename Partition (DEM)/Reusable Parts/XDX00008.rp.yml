Reusable Part:
  Name:               XDXCarousel
  Enabled For RDMLX:  Yes
  Target Platform:    Windows/IBM i/Linux

  Framework:
    Id1:                10240
    Id2:                30

  Multilingual Details:
  - ENG:
      Description:        "Carousel"

  - FRA:
      Description:        "Carrousel"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #XDXBasePanel) Height(444) Layoutmanager(#Layout) Width(628)
    
    * User Designed Control - Carousel
    * Individual items are made by adding entries as per typical LANSA list processing
    * Fields in the list are defined by the *ListFields parameter of the Design being made
    Define_Com Class(#prim_panl) Name(#BackGround) Displayposition(1) Height(444) Layoutmanager(#Layout) Left(0) Parent(#COM_OWNER) Style(#XDXStyles<ContentBackground>) Tabposition(1) Top(0) Width(628)
    
    Define_Com Class(#prim_caro<#XDXCarouselDesign>) Name(#Carousel) Displayposition(1) Height(302) Left(6) Navigationstyle(None) Parent(#BackGround) Tabposition(1) Top(6) Width(616)
    
    Define_Com Class(#prim_tkbr) Name(#TrackBar) Displayposition(2) Left(20) Parent(#BackGround) Tabposition(2) Tickstyle(None) Top(314) Value(1) Width(588)
    
    Define_Com Class(#prim_atlm) Name(#Layout)
    
    Define_Com Class(#PRIM_ATLI) Name(#ATLI_1) Attachment(Center) Manage(#BackGround) Parent(#Layout)
    Define_Com Class(#PRIM_ATLI) Name(#ATLI_2) Attachment(Center) Manage(#Carousel) Marginbottom(6) Marginleft(6) Marginright(6) Margintop(6) Parent(#Layout)
    Define_Com Class(#PRIM_ATLI) Name(#ATLI_3) Attachment(Bottom) Manage(#TrackBar) Marginbottom(80) Marginleft(20) Marginright(20) Parent(#Layout)
    
    Define_Com Class(#Prim_timr) Name(#TrackBarTimer) Interval(300)
    
    Evtroutine Handling(#Com_owner.CreateInstance)
    
    #TrackBarTimer.Stop
    
    #Com_owner.Load
    
    Endroutine
    
    Mthroutine Name(Load) Help("Create carousel items") Access(*Private)
    
    * Create Carousel Items
    Select Fields(#Surname #Givename #empno) From_File(pslmst)
    
    Fetch Fields(#empimg) From_File(pslimg) With_Key(#Empno)
    
    Add_Entry To_List(#Carousel)
    
    Endselect
    
    #TrackBar.MinimumValue := 1
    #TrackBar.maximumValue := #Carousel.Items.ItemCount
    
    Endroutine
    
    Evtroutine Handling(#TrackBar.Changed)
    
    #TrackBarTimer.Stop
    #TrackBarTimer.Start
    
    Endroutine
    
    Evtroutine Handling(#Carousel.ItemGotFocus) Item(#Item)
    
    #TrackBar.Value := #Item.Entry
    
    Endroutine
    
    Evtroutine Handling(#TrackBarTimer.Tick)
    
    #TrackBarTimer.Stop
    
    #Carousel.Items.Item<#TrackBar.Value>.Focus := True
    
    Endroutine
    
    End_Com
    
    
