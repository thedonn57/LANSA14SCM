Reusable Part:
  Name:               XETMessages
  Enabled For RDMLX:  Yes
  Target Platform:    Browser

  Framework:
    Id1:                10240
    Id2:                30

  Multilingual Details:
  - ENG:
      Description:        "Employee Messages Popup"

  - FRA:
      Description:        "Employee Messages Popup"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_PPNL) Left(0) Height(320) Top(0) Width(312) Layoutmanager(#TableLayout) Shroudstyle(#StyleShroud) Autoclose(False)
    
    * Styles
    Define_Com Class(#PRIM_VS.Style) Name(#StyleShroud) Backgroundbrush(#BrushShroud)
    Define_Com Class(#PRIM_VS.SolidBrush) Name(#BrushShroud) Color(Theme200) Opacity(5)
    Define_Com Class(#PRIM_VS.Style) Name(#Style1) Borderbottom(0) Borderleft(0) Borderright(0) Bordertop(0)
    
    * Layout
    Define_Com Class(#PRIM_TBLO) Name(#TableLayout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#Column1) Displayposition(1) Parent(#TableLayout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Row1) Displayposition(2) Parent(#TableLayout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Row2) Displayposition(3) Parent(#TableLayout) Height(56) Units(Pixels)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Row3) Displayposition(1) Parent(#TableLayout) Height(48) Units(Pixels)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem2) Column(#Column1) Flow(Right) Manage(#ButtonOk) Parent(#TableLayout) Row(#Row2) Alignment(CenterRight) Margintop(3) Marginbottom(3) Marginleft(3) Marginright(3)
    
    * Controls - A label and two buttons
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem1) Column(#Column1) Manage(#List1) Parent(#TableLayout) Row(#Row1) Margintop(10)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem3) Alignment(TopLeft) Column(#Column1) Flow(Down) Manage(#Label1) Parent(#TableLayout) Row(#Row3)
    
    Define_Com Class(#PRIM_PHBN) Name(#ButtonOk) Caption('OK') Displayposition(1) Parent(#COM_OWNER) Tabposition(1) Top(265) Height(50) Width(304) Left(3)
    Define_Com Class(#PRIM_LIST) Name(#List1) Displayposition(2) Height(204) Parent(#COM_OWNER) Tabposition(2) Top(58) Width(310) Columnheaderheight(0) Rowheight(40) Style(#Style1) Columnlines(False) Rowlines(False) Left(0)
    Define_Com Class(#PRIM_LIST.String) Name(#ColumnString) Displayposition(1) Parent(#List1) Cellmarginleft(10)
    Define_Com Class(#PRIM_LABL) Name(#Label1) Displayposition(3) Ellipses(Word) Height(48) Left(0) Parent(#COM_OWNER) Tabposition(3) Tabstop(False) Top(0) Verticalalignment(Center) Width(310) Themedrawstyle('MediumTitle') Caption('Save Failed') Marginleft(20)
    
    * ******
    * Events
    * ******
    
    Define_Evt Name(OkClicked)
    
    Define_Evt Name(CancelClicked)
    
    * **************
    * Event Handlers
    * **************
    
    Evtroutine Handling(#ButtonOk.Click)
    
    #COM_OWNER.ClosePopup
    
    Signal Event(OkClicked)
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Closed)
    
    Clr_List
    
    #SYS_MSGQ.ClearAll
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Prepare)
    
    For Each(#Message) In(#SYS_MSGQ.Messages)
    
    Add_Entry
    
    #ColumnString.CurrentItem.Value := #Message.Text
    
    Endfor
    
    #ColumnString.SizeToContents
    
    If (#ColumnString.ColumnWidth < #List1.Width - 3)
    
    #ColumnString.ColumnWidth := #List1.Width - 3
    
    Endif
    
    Endroutine
    
    End_Com
