Reusable Part:
  Name:               xDemoDropDown
  Enabled For RDMLX:  Yes
  Target Platform:    Windows/IBM i/Linux

  Framework:
    Id1:                10240
    Id2:                30

  Multilingual Details:
  - ENG:
      Description:        "Drop Down"

  - FRA:
      Description:        "Drop Down Simple"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_PANL) Displayposition(1) Height(44) Left(0) Tabposition(1) Top(0) Width(268) Layoutmanager(#Layout) Tabstop(False)
    
    * Sample Drop down ready to be used on a form
    
    Define_Com Class(#PRIM_TBLO) Name(#Layout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Row_1) Displayposition(1) Parent(#Layout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#Column_1) Displayposition(1) Parent(#Layout)
    
    * Dropdown using a simple image and caption design
    Define_Com Class(#PRIM_CMBX) Name(#DropDown) Displayposition(1) Height(44) Left(0) Parent(#COM_OWNER) Tabposition(1) Top(0) Width(268) Fixedheight(False) Comboboxstyle(DropDownList)
    Define_Com Class(#PRIM_TBLO.Item) Name(#TBLO_1) Column(#Column_1) Parent(#Layout) Row(#Row_1) Manage(#DropDown)
    Define_Com Class(#PRIM_CBCL) Name(#CBCL1) Displayposition(1) Parent(#DropDown) Source(#xDemoCaption) Usepicklist(False)
    Define_Com Class(#PRIM_CBCL) Name(#CBCL2) Parent(#DropDown) Source(#xDemoValue) Usepicklist(False) Visible(False)
    
    * Dropdown is published as a property allowing its features to be modified as required
    Define_Pty Name(DropDown) Get(*reference #DropDown)
    
    * Dropdown Current Item
    Define_Pty Name(CurrentItem) Get(GetCurrentItem)
    
    * Dropdown Focus Item
    Define_Pty Name(FocusItem) Get(GetFocusItem)
    
    Define_Evt Name(ItemGotFocus)
    Define_Map For(*Input) Class(#xDemoValue) Name(#Value) Help('Value of the focus item')
    Define_Map For(*Input) Class(#xDemoCaption) Name(#Caption) Help('Caption of the focus item')
    Define_Map For(*Input) Class(#prim_cbit) Name(#Item) Pass(*by_reference)
    
    Ptyroutine Name(GetCurrentItem)
    Define_Map For(*Output) Class(#prim_cbit) Name(#Property) Pass(*by_reference)
    
    #Property <= #DropDown.CurrentItem
    
    Endroutine
    
    Ptyroutine Name(GetFocusItem)
    Define_Map For(*Output) Class(#prim_cbit) Name(#Property) Pass(*by_reference)
    
    #Property <= #DropDown.FocusItem
    
    Endroutine
    
    Mthroutine Name(Add) Help('Add an entry to the drop down')
    Define_Map For(*Input) Class(#xDemoValue) Name(#Value) Help('Value of the item e.g. An item code or identifier')
    Define_Map For(*Input) Class(#xDemoCaption) Name(#Caption) Help('Text to display for the item')
    Define_Map For(*Input) Class(#Prim_objt) Name(#Reference) Mandatory(*Null) Help('Reference to store against the item') Pass(*by_reference)
    Define_Map For(*Result) Class(#prim_cbit) Name(#Result) Help('Reference to the newly created item') Pass(*by_reference)
    
    * Apply the supplied values
    #xDemoValue := #Value
    #xDemoCaption := #Caption
    
    Add_Entry To_List(#DropDown)
    
    * Store the supplied data object against the item
    #DropDown.CurrentItem.RelatedReference <= #Reference
    
    #Result <= #DropDown.CurrentItem
    
    Endroutine
    
    Mthroutine Name(Clear)
    
    Clr_List Named(#DropDown)
    
    Endroutine
    
    Mthroutine Name(Find) Help('Find the first item with the value specified in the Value parameter')
    Define_Map For(*Input) Class(#xDemoValue) Name(#Value) Help('Value to search for')
    Define_Map For(*Input) Class(#Prim_boln) Name(#Focus) Mandatory(True) Help('Set focus to the found item?')
    Define_Map For(*Result) Class(#prim_cbit) Name(#Result) Help('Reference to the found item') Pass(*by_reference)
    
    * Look through the drop down items for one matching the supplied value
    Selectlist Named(#DropDown)
    Continue If(#xDemoValue <> #Value)
    
    #Result <= #DropDown.CurrentItem
    
    If (#Focus)
    
    #DropDown.CurrentItem.Focus := True
    
    Endif
    
    Leave
    Endselect
    
    Endroutine
    
    Mthroutine Name(FindReference) Help('Find the first item with the value specified in the Value parameter')
    Define_Map For(*Input) Class(#Prim_objt) Name(#Reference) Help('Reference to store against the item') Pass(*by_reference)
    Define_Map For(*Input) Class(#Prim_boln) Name(#Focus) Mandatory(True) Help('Set focus to the found item?')
    Define_Map For(*Result) Class(#prim_cbit) Name(#Result) Help('Reference to the found item') Pass(*by_reference)
    
    * Search the items RelatedReference property for the supplied object
    For Each(#Item) In(#DropDown.items)
    
    Continue If(#Item.RelatedReference *IsNotEqualTo #Reference)
    
    #Result <= #Item
    
    Leave
    
    Endfor
    
    If ((#Focus) *AndIf (#Result *IsNot *null))
    
    #Result.Focus := True
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#DropDown.ItemGotFocus)
    
    Signal Event(ItemGotFocus) Value(#xDemoValue) Caption(#xDemoCaption) Item(#DropDown.CurrentItem)
    
    Endroutine
    
    End_Com
