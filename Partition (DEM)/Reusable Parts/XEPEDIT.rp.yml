Reusable Part:
  Name:               XEPEditor
  Enabled For RDMLX:  Yes
  Target Platform:    Browser

  Framework:
    Id1:                10240
    Id2:                30

  Multilingual Details:
  - ENG:
      Description:        "Employee Editor"

  - FRA:
      Description:        "Employee Editor"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_PANL) Displayposition(1) Height(569) Left(0) Tabposition(1) Top(0) Width(500) Layoutmanager(#TableLayout) Style(#Style_WhiteBackground)
    
    * Style
    Define_Com Class(#PRIM_VS.Style) Name(#Style_WhiteBackground) Normbackcolor(White)
    
    * Layout
    Define_Com Class(#PRIM_TBLO) Name(#TableLayout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#Column1) Displayposition(1) Parent(#TableLayout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Row1) Displayposition(1) Parent(#TableLayout) Height(48) Units(Pixels)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Row2) Displayposition(2) Parent(#TableLayout)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem1) Alignment(CenterLeft) Column(#Column1) Parent(#TableLayout) Row(#Row1)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem4) Column(#Column1) Manage(#EmployeeEditor) Parent(#TableLayout) Row(#Row2)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem6) Column(#Column1) Manage(#ImageComplete) Parent(#TableLayout) Row(#Row1) Sizing(FitToHeight) Alignment(TopRight) Flow(Left)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem7) Column(#Column1) Parent(#TableLayout) Row(#Row1) Sizing(FitToHeight) Alignment(TopRight) Flow(Left) Manage(#ImageCamera)
    Define_Com Class(#PRIM_TBLO.Item) Name(#PanelHeadingItem1) Column(#Column1) Manage(#PanelHeading) Parent(#TableLayout) Row(#Row1)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem8) Column(#Column1) Manage(#ImageCancel) Parent(#TableLayout) Row(#Row1) Sizing(FitToHeight) Alignment(CenterLeft) Flow(Right)
    
    * A Heading Panel, with a LABEL and Cancel/Camera/Complete buttons on it
    Define_Com Class(#PRIM_PANL) Name(#PanelHeading) Displayposition(2) Height(48) Left(0) Parent(#COM_OWNER) Tabposition(1) Tabstop(False) Top(0) Width(500) Themedrawstyle('MediumTitle') Layoutmanager(#TableLayout)
    Define_Com Class(#PRIM_LABL) Name(#LabelHeading) Displayposition(4) Ellipses(Word) Height(48) Left(0) Parent(#PanelHeading) Tabposition(1) Tabstop(False) Top(0) Verticalalignment(Center) Width(500) Themedrawstyle('Heading1+MediumTitle') Marginleft(50) Caption('New Employee')
    Define_Com Class(#PRIM_IMAG) Name(#ImageCancel) Displayposition(3) Left(0) Parent(#PanelHeading) Tabstop(False) Top(0) Height(48) Tabposition(2) Image(#XEPBM008) Width(48)
    Define_Com Class(#PRIM_WEB.FilePicker) Name(#ImageCamera) Displayposition(2) Left(404) Parent(#PanelHeading) Tabposition(3) Tabstop(False) Top(0) Height(48) Width(48) Image(#XEPBM009) Alignment(Center) Verticalalignment(Center)
    Define_Com Class(#PRIM_IMAG) Name(#ImageComplete) Displayposition(1) Left(452) Parent(#PanelHeading) Tabposition(4) Tabstop(False) Top(0) Height(48) Width(48) Image(#XEPBM007)
    
    * A Reusable Part to show all employee details
    Define_Com Class(#XEPDetails) Name(#EmployeeEditor) Height(521) Parent(#COM_OWNER) Tabposition(2) Tabstop(False) Top(48)
    
    * A reference to the employee we are editing, published as a property
    Define_Com Class(#XEPData) Name(#CurrentEmployee) Reference(*DYNAMIC)
    Define_Pty Name(CurrentEmployee) Get(*REFERENCE #CurrentEmployee)
    
    * ****** *
    * Events *
    * ****** *
    
    Define_Evt Name(EditCompleted)
    
    Define_Evt Name(EditCancelled)
    
    Define_Evt Name(EditFailed)
    
    * ************** *
    * Event Handlers *
    * ************** *
    
    Evtroutine Handling(#ImageCancel.Click)
    
    Signal Event(EditCancelled)
    
    #CurrentEmployee <= *NULL
    
    Endroutine
    
    Evtroutine Handling(#ImageCamera.FileSelected) File(#File)
    
    #EmployeeEditor.UpdateEmployeeImage( #File.Blob )
    
    Endroutine
    
    Evtroutine Handling(#ImageComplete.Click)
    
    #COM_OWNER.Enabled := False
    
    #EmployeeEditor.SaveEmployee( #CurrentEmployee )
    
    Endroutine
    
    Evtroutine Handling(#EmployeeEditor.SaveSucceeded)
    
    #COM_OWNER.Enabled := True
    
    Signal Event(EditCompleted)
    
    #CurrentEmployee <= *NULL
    
    Endroutine
    
    Evtroutine Handling(#EmployeeEditor.SaveFailed)
    
    #COM_OWNER.Enabled := True
    
    Signal Event(EditFailed)
    
    Endroutine
    
    * **************
    * Public Methods
    * **************
    
    Mthroutine Name(AddEmployee)
    
    #LabelHeading := "New Employee"
    
    #CurrentEmployee <= #EmployeeEditor.AddEmployee
    
    Endroutine
    
    Mthroutine Name(EditEmployee)
    Define_Map For(*INPUT) Class(#XEPData) Name(#Employee) Pass(*BY_REFERENCE)
    
    #CurrentEmployee <= #Employee
    
    #LabelHeading := "Edit Employee"
    
    #EmployeeEditor.EditEmployee( #CurrentEmployee )
    
    Endroutine
    
    End_Com
