Form:
  Name:               xDemoFilteringListData
  Enabled For RDMLX:  Yes

  Framework:
    Id1:                10240
    Id2:                30

  Multilingual Details:
  - ENG:
      Description:        "Filtering data in a list"

  - FRA:
      Description:        "Filtering data in a list"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_FORM) Caption('Filter data in a list') Clientwidth(587) Clientheight(476) Left(188) Top(205) Layoutmanager(#Layout) Height(515) Width(603) Theme(#xDemoTheme)
    
    * Example of showing and hiding list items based on data input
    
    Define_Com Class(#PRIM_TBLO) Name(#Layout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Row_1) Displayposition(1) Parent(#Layout) Height(26) Units(Pixels)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Row_2) Displayposition(2) Parent(#Layout) Height(1.79)
    Define_Com Class(#PRIM_TBLO.Column) Name(#Column_1) Displayposition(1) Parent(#Layout)
    Define_Com Class(#SURNAME.Visual) Name(#Filter) Displayposition(1) Parent(#COM_OWNER) Tabposition(1) Width(585) Labeltype(Caption) Top(2) Left(2) Height(22) Caption('Find ') Marginleft(80)
    Define_Com Class(#PRIM_TBLO.Item) Name(#TBLO_1) Alignment(TopLeft) Column(#Column_1) Flow(Down) Manage(#Filter) Parent(#Layout) Row(#Row_1) Margintop(2) Marginbottom(2) Marginleft(2)
    Define_Com Class(#PRIM_TRVW) Name(#List) Columnbuttonheight(19) Componentversion(2) Displayposition(2) Height(448) Keyboardpositioning(SortColumn) Left(0) Parent(#COM_OWNER) Tabposition(2) Top(28) Width(587) Viewstyle(UnLevelled) Linesatroot(False) Fullrowselect(True)
    Define_Com Class(#PRIM_TBLO.Item) Name(#TBLO_2) Column(#Column_1) Manage(#List) Parent(#Layout) Row(#Row_2) Margintop(2)
    Define_Com Class(#PRIM_TVCL) Name(#TVCL_1) Displayposition(1) Level(1) Parent(#List) Source(#EMPNO) Width(26) Sortonclick(True)
    Define_Com Class(#PRIM_TVCL) Name(#TVCL_2) Displayposition(3) Level(2) Parent(#List) Source(#SURNAME) Width(36) Sortonclick(True)
    Define_Com Class(#PRIM_TVCL) Name(#TVCL_3) Displayposition(2) Level(3) Parent(#List) Source(#GIVENAME) Width(37) Sortonclick(True)
    
    * Edit timer used to delay a changed event for a field so that we don't update every keypress
    Define_Com Class(#xDemoEditTimer) Name(#Timer)
    
    Evtroutine Handling(#com_owner.Createinstance)
    
    * Load the list data
    Select Fields(#List) From_File(pslmst)
    
    Add_Entry To_List(#List)
    
    Endselect
    
    Endroutine
    
    Evtroutine Handling(#Filter.Changed)
    
    * Reset the timer every time there's a key press
    * This allow keyboard entry without updating the list every time
    #Timer.Restart
    
    Endroutine
    
    Evtroutine Handling(#Timer.Ticked)
    
    * Show/hide items based on the value entered
    Selectlist Named(#List)
    
    * Visible if EMPNO, SURNAME or GIVENAME contain the filter value
    #List.currentitem.visible := #Empno.lowercase.Contains( #Filter.lowercase ) Or #Givename.lowercase.Contains( #Filter.lowercase ) Or #Surname.lowercase.Contains( #Filter.lowercase ) Or (#Filter = "")
    
    Endselect
    
    Endroutine
    
    End_Com
