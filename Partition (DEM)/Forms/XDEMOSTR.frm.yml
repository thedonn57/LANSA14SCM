Form:
  Name:               xDemoStringToWords
  Enabled For RDMLX:  Yes

  Framework:
    Id1:                10240
    Id2:                30

  Multilingual Details:
  - ENG:
      Description:        "Convert a String to Individual Word"

  - FRA:
      Description:        "Convert a String to Individual Word"

  Source: |
    Function Options(*DIRECT)
    Begin_Com Role(*EXTENDS #PRIM_FORM) Caption('Convert text to words') Clientheight(419) Clientwidth(912) Height(458) Layoutmanager(#TableLayout_1) Left(98) Top(197) Width(928)
    
    * String array handling
    * Split a string into a collection of "words"
    
    Define_Com Class(#PRIM_TBLO) Name(#TableLayout_1) Dividerstyle(Gap)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Row_1) Displayposition(1) Parent(#TableLayout_1)
    Define_Com Class(#PRIM_TBLO.Column) Name(#Column_1) Displayposition(1) Parent(#TableLayout_1) Width(1.49)
    Define_Com Class(#PRIM_TBLO.Column) Name(#Column_2) Displayposition(3) Parent(#TableLayout_1) Width(0.51)
    Define_Com Class(#PRIM_TBLO.Item) Name(#TBLO_2) Column(#Column_2) Manage(#WordList) Parent(#TableLayout_1) Row(#Row_1)
    Define_Com Class(#PRIM_TBLO.ColumnDivider) Name(#ColumnDivider_1) Displayposition(2) Parent(#TableLayout_1) Width(4)
    Define_Com Class(#PRIM_TBLO.Item) Name(#TBLO_1) Column(#Column_1) Manage(#String) Parent(#TableLayout_1) Row(#Row_1)
    
    Define_Com Class(#PRIM_TRVw) Name(#WordList) Columnbuttonheight(19) Columnheaders(False) Componentversion(2) Displayposition(1) Fullrowselect(True) Height(419) Keyboardpositioning(SortColumn) Left(681) Linesatroot(False) Parent(#COM_OWNER) Tabposition(1) Top(0) Viewstyle(UnLevelled) Width(231)
    Define_Com Class(#PRIM_TVCL) Name(#TVCL_1) Captiontype(Caption) Displayposition(1) Level(1) Parent(#WordList) Source(#xDemoUnicode128) Width(97) Widthtype(Remainder)
    
    Define_Com Class(#xDemoUnicode4K.MultiLineEdit) Name(#String) Displayposition(2) Height(419) Marginleft(0) Parent(#COM_OWNER) Tabposition(2) Width(677)
    
    * Utilities - Variety of commonly used methods
    Define_Com Class(#xDemoUtilities) Name(#Utilities)
    
    * Edit timer used to delay updates until a pause
    Define_Com Class(#xDemoEditTimer) Name(#Timer)
    
    Evtroutine Handling(#com_owner.CreateInstance)
    
    #String := "Enter some text here.  When you stop typing it will be converted to words"
    
    Endroutine
    
    Evtroutine Handling(#String.Changed)
    
    * Stop and restart the timer every time the string is changed
    * When the user stops making changes for long enough the timer will fire
    
    #Timer.Restart
    
    Endroutine
    
    Evtroutine Handling(#Timer.Ticked)
    
    Clr_List Named(#WordList)
    
    * Iterate through the "words" in the string.
    For Each(#Word) In(#Utilities.SplitString( #String ))
    
    #Com_Owner.AddwordListEntry( #Word )
    
    Endfor
    
    * Add a count as the top item
    #Com_Owner.AddwordListEntry( ("&1 Words").Substitute( #WordList.Entries.Asstring ) )
    #WordList.CurrentItem.Position := 1
    
    Endroutine
    
    Mthroutine Name(AddWordListEntry) Access(*Private)
    Define_Map For(*Input) Class(#xDemoUnicode128) Name(#Word)
    
    #xDemoUnicode128 := #Word
    Add_Entry To_List(#WordList)
    
    Endroutine
    
    End_Com
