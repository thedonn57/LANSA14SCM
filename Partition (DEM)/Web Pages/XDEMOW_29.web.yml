Web Page:
  Name:               xDemoWebPositionTo
  Enabled For RDMLX:  Yes

  Framework:
    Id1:                10240
    Id2:                30

  Multilingual Details:
  - ENG:
      Description:        "Positioning to a list item"

  - FRA:
      Description:        "Positioning to a list item"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_WEB) Layoutmanager(#Layout) Theme(#xDemoTheme)
    
    Define_Com Class(#PRIM_TBLO) Name(#Layout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Row_1) Displayposition(1) Parent(#Layout) Height(31) Units(Pixels)
    Define_Com Class(#PRIM_TBLO.Row) Name(#Row_2) Displayposition(2) Parent(#Layout) Height(1.79)
    Define_Com Class(#PRIM_TBLO.Column) Name(#Column_1) Displayposition(1) Parent(#Layout)
    Define_Com Class(#SURNAME.Visual) Name(#Filter) Displayposition(1) Parent(#COM_OWNER) Tabposition(1) Width(344) Labeltype(Caption) Top(2) Left(856) Height(27) Caption('Find ') Marginleft(0) Placeholder('Search') Promptertabstop(False) Prompterimage(#xImageSearch22) Showprompter(True)
    Define_Com Class(#PRIM_TBLO.Item) Name(#TBLO_1) Alignment(CenterRight) Column(#Column_1) Manage(#Filter) Parent(#Layout) Row(#Row_1) Margintop(2) Marginbottom(2) Marginleft(2) Sizing(FitToHeight) Marginright(2)
    Define_Com Class(#PRIM_LIST) Name(#List) Displayposition(2) Height(769) Left(0) Parent(#COM_OWNER) Tabposition(2) Top(31) Width(1200) Columnlines(False) Rowlines(False) Rowheight(25)
    Define_Com Class(#PRIM_TBLO.Item) Name(#LayoutItem) Column(#Column_1) Manage(#List) Parent(#Layout) Row(#Row_2)
    Define_Com Class(#PRIM_LIST.String) Name(#ColumnEMPNO) Displayposition(2) Parent(#List) Source(#EMPNO) Columnwidth(117) Sortonclick(True)
    Define_Com Class(#PRIM_LIST.String) Name(#ColumnSURNAME) Displayposition(3) Parent(#List) Source(#SURNAME) Columnwidth(330) Sortonclick(True)
    Define_Com Class(#PRIM_LIST.String) Name(#ColumnGIVENAME) Displayposition(4) Parent(#List) Source(#GIVENAME) Columnwidth(344) Sortonclick(True)
    Define_Com Class(#PRIM_LIST.Image) Name(#ColumnImage) Columnwidth(50) Displayposition(1) Parent(#List) Imagesizing(BestFit) Cellmarginbottom(2) Cellmargintop(2) Source(#EMPTHM)
    
    Define_Com Class(#xDemoEditTimer) Name(#EditTimer)
    
    * List of employees so store data returned from the server
    Def_List Name(#Employees) Fields(#Empno #Surname #Givename #Address1 #Address2 #Address3 #Postcode #Phonehme #Phonebus #Deptment #Section #Salary #Startdte #Termdate #Empthm) Type(*working) Entrys(999)
    
    Evtroutine Handling(#com_owner.Createinstance)
    
    #Com_owner.Load
    
    #com_owner.caption := 'Filter data in a list'
    
    Endroutine
    
    Mthroutine Name(Load)
    
    * Define the service instance and execute asynchornously
    Define_Com Class(#xDemoDataServices.GetEmployees) Name(#GetEmployees)
    
    #GetEmployees.ExecuteAsync( #Employees )
    
    * When the server returns
    Evtroutine Handling(#GetEmployees.Completed)
    
    * Load the list data
    Selectlist Named(#Employees)
    
    Add_Entry To_List(#List)
    
    Endselect
    
    Endroutine
    
    Endroutine
    
    Evtroutine Handling(#Filter.Changed)
    
    * Stop and start the delay timer every key press
    * This allow keyboard entry without updating the list every time
    #EditTimer.Restart
    
    Endroutine
    
    Evtroutine Handling(#EditTimer.Ticked)
    
    Selectlist Named(#List)
    
    If ((#Empno.uppercase.Contains( #Filter.uppercase )) *Or (#Surname.uppercase.Contains( #Filter.uppercase )) *Or (#Givename.uppercase.Contains( #Filter.uppercase )))
    
    #List.VerticalScrollTo( #List.CurrentItem Top )
    
    #List.currentItem.Focus := True
    
    Leave
    
    Endif
    
    Endselect
    
    Endroutine
    
    End_Com
